apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: '../ktlint.gradle'
apply from: '../kococo.gradle'


def GIT_SHA = "GIT_SHA"
def BUILD_TIME = "BUILD_TIME"
def API_URL = "API_URL"
def IMAGE_API_URL = "IMAGE_API_URL"

android {
    compileSdkVersion androidSdk.compile

    defaultConfig {
        minSdkVersion androidSdk.min
        targetSdkVersion androidSdk.target

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            testCoverageEnabled true
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    productFlavors.whenObjectAdded { flavor ->
        def version = rootProject.ext.version

        flavor.versionCode generateAutoVersionCode()
        flavor.versionName generateVersionName(version)
    }

    flavorDimensions "default"
    productFlavors {
        demo {
            dimension "default"
        }
    }

    project.android.applicationVariants.all { variant ->
        variant.productFlavors.each { flavor ->

            def flavorData = rootProject.ext[flavor.name]
            def appName = flavorData.appName
            def appId = getVariantFieldValue(flavorData.applicationId, variant.buildType.name)
            def version = generateVersionName(rootProject.ext.version)
            def code = generateAutoVersionCode()
            //def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
            def buildTime = new GregorianCalendar().format("MM-dd-yyyy' 'h:mm:ss a z")

            if (variant.buildType.name != "release") {
                def prefix = ""
                if (variant.buildType.name == "debug") {
                    //gitSha = "DEBUG"
                    buildTime = "DEBUG"
                    prefix = "[D] "
                } else if (variant.buildType.name == "staging") {
                    //gitSha = "STAGING"
                    buildTime = "STAGING"
                    prefix = "[S] "
                }

                appName = prefix + appName + "_$variant.buildType.name" + "_$version" + "($code)"
            }

            setVariantApplicationId(variant, appId)
            setVariantResValue(variant, appName, "app_name", STRING.toLowerCase())

            //setVariantBuildConfigField(variant, gitSha, GIT_SHA, STRING)
            setVariantBuildConfigField(variant, buildTime, BUILD_TIME, STRING)
            setVariantBuildConfigField(variant, flavorData.apiUrl, API_URL, STRING)
            setVariantBuildConfigField(variant, flavorData.imageApiUrl, IMAGE_API_URL, STRING)
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:$rootProject.libraries.appcompat"
    implementation "androidx.legacy:legacy-support-v4:$rootProject.libraries.legacySupport"
    implementation "com.google.android.material:material:$rootProject.libraries.material"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.libraries.constraint"
    implementation "androidx.recyclerview:recyclerview:$rootProject.libraries.recyclerView"

    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.libraries.lifecycle"
    kapt "androidx.lifecycle:lifecycle-compiler:$rootProject.libraries.lifecycle"
    implementation "androidx.room:room-runtime:$rootProject.libraries.room"
    kapt "androidx.room:room-compiler:$rootProject.libraries.room"
    implementation "android.arch.navigation:navigation-fragment:$rootProject.libraries.navigation"
    implementation "android.arch.navigation:navigation-ui:$rootProject.libraries.navigation"

    implementation "androidx.core:core-ktx:$rootProject.libraries.ktx"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation "junit:junit:$rootProject.testLibraries.junit"
    androidTestImplementation "androidx.test:runner:$rootProject.testLibraries.runner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.testLibraries.espressoCore"

    // Test helpers for LiveData
    testImplementation "androidx.arch.core:core-testing:$rootProject.libraries.lifecycle"

    // Test helpers for Room
    testImplementation "androidx.room:room-testing:$rootProject.libraries.room"
}
